(* SM + RpV-SUSY UDD Interaction                                                               *)
(*      Based on the Standard Model file in FeynRules6, written by N. Christensen and C. Duhr. *)


If[Or[Not[ValueQ[M$ModelName]], M$ModelName != "Standard Model"], Print["[ERROR] Standard Model must be loaded."]; Abort[]]

M$ModelName = "SM_plus_UDD";

M$Information = {Authors      -> "Sho Iwamoto",
                 Version      -> "0.1",
                 Date         -> "Apr, 5. 2011",
                 Institutions -> "The University of Tokyo",
                 Emails       -> "iwamoto@hep-th.phys.s.u-tokyo.ac.jp"
                };

(*****************************************************************************************)

IndexRange[Index[SCA]] = Range[6]; IndexStyle[SCA, i];

(*****************************************************************************************)

M$Parameters = {
  MsqL   == {
        ParameterType -> External,
        TeX           -> Subsuperscript["M","sq","L"],
        Value         -> 200,
        Description   -> "Left-handed Squark mass in GeV"},
  MsqR   == {
        ParameterType -> External,
        TeX           -> Subsuperscript["M","sq","R"],
        Value         -> 200,
        Description   -> "Right-handed Squark mass in GeV"},
  Wsq    == {
        ParameterType -> External,
        TeX           -> Subscript["W","sq"],
        Value         -> 0,
        Description   -> "Squark width in GeV"},
  UDD312 == {
        ParameterType -> External,
        InteractionOrder -> {RPV, 1},
        TeX -> Subsuperscript["\[Lambda]", "312", "\[DoublePrime]"],
        Value -> 1.0,
        Description -> "RpV-UDD312 coupling constant" },
  UDD313 == {
        ParameterType -> External,
        InteractionOrder -> {RPV, 1},
        TeX -> Subsuperscript["\[Lambda]", "313", "\[DoublePrime]"],
        Value -> 1.0,
        Description -> "RpV-UDD313 coupling constant" }
}

(*****************************************************************************************)

M$ClassesDescription = {
  S[4] == {
        ClassName        -> su,
        SelfConjugate    -> False,
        Indices          -> {Index[SCA], Index[Colour]},
        FlavorIndex      -> SCA,
        QuantumNumbers   -> { Q -> 2/3 },
        PDG              ->        { 1000002  ,  1000004  ,  1000006  ,  2000002  ,  2000004  ,  2000006   },
        ParticleName     ->        { "suL"    ,  "scL"    ,  "stL"    ,  "suR"    ,  "scR"    ,  "stR"     },
        AntiParticleName ->        { "suL~"   ,  "scL~"   ,  "stL~"   ,  "suR~"   ,  "scR~"   ,  "stR~"    },
        ClassMembers     ->        {  suL     ,   scL     ,   stL     ,   suR     ,   scR     ,   stR      },
        Mass             -> {{MsqL,Internal},{MsqL,Internal},{MsqL,Internal},{MsqR,Internal},{MsqR,Internal},{MsqR,Internal}},
        Width            -> {{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal}},
        PropagatorLabel  -> { "su",  "suL"    ,  "scL"    ,  "stL"    ,  "suR"    ,  "scR"    ,  "stR"     },
        FullName         -> { "u-squark-left", "c-squark-left", "t-squark-left", "u-squark-right", "c-squark-right", "t-squark-right"},
        PropagatorType   -> ScalarDash,
        PropagatorArrow  -> Forward
       },
  S[5] == {
        ClassName        -> sd,
        SelfConjugate    -> False,
        Indices          -> {Index[SCA], Index[Colour]},
        FlavorIndex      -> SCA,
        QuantumNumbers   -> { Q -> -1/3 },
        PDG              ->        { 1000001, 1000003, 1000005, 2000001, 2000003, 2000005 },
        ParticleName     ->        { "sdL"  , "ssL"  , "sbL"  , "sdR"  , "ssR"  , "sbR"   },
        AntiParticleName ->        { "sdL~" , "ssL~" , "sbL~" , "sdR~" , "ssR~" , "sbR~"  },
        ClassMembers     ->        {  sdL   ,  ssL   ,  sbL   ,  sdR   ,  ssR   ,  sbR    },
        Mass             -> {{MsqL,Internal},{MsqL,Internal},{MsqL,Internal},{MsqR,Internal},{MsqR,Internal},{MsqR,Internal}},
        Width            -> {{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal},{Wsq, Internal}},
        PropagatorLabel  -> { "sd",  "sdL"  , "ssL"  , "sbL"  , "sdR"  , "ssR"  , "sbR"   },
        FullName         -> { "d-squark-left", "s-squark-left", "b-squark-left", "d-squark-right", "s-squark-right", "b-squark-right"},
        PropagatorType   -> ScalarDash,
        PropagatorArrow  -> Forward
       }
}

(*****************************************************************************************)

LRPV := Module[{udd,uddCC,Msquark, g,ga,gb, c,c1,c2,c3, sp,i,j,k,mu},
         (*** UDD Terms Definition ***)
         udd[i_,j_,k_]   := Eps[c1,c2,c3]( HC[su[i+3, c1]]   dqbar[sp, j, c2] .left[CC[dq[sp, k, c3]]]
                                         + HC[sd[j+3, c2]]   uqbar[sp, i, c1] .left[CC[dq[sp, k, c3]]]
                                         + HC[sd[k+3, c3]]   uqbar[sp, i, c1] .left[CC[dq[sp, j, c2]]]);
         uddCC[i_,j_,k_] := Eps[c1,c2,c3](    su[i+3, c1] CC[dqbar[sp, j, c2]].right[  dq[sp, k, c3]]
                                            + sd[j+3, c2] CC[uqbar[sp, i, c1]].right[  dq[sp, k, c3]]
                                            + sd[k+3, c3] CC[uqbar[sp, i, c1]].right[  dq[sp, j, c2]]);
         Msquark[i_] := If[i<4, MsqL, MsqR];
         (*** The Lagrangian ***)
         HC[DC[su, mu]] DC[su, mu]+HC[DC[sd, mu]] DC[sd, mu]
         - Msquark[i]^2 HC[su[i,c]].su[i,c]
         - Msquark[i]^2 HC[sd[i,c]].sd[i,c]
         - UDD312 udd[3,1,2] - Conjugate[UDD312] uddCC[3,1,2]
         - UDD313 udd[3,1,3] - Conjugate[UDD313] uddCC[3,1,3]
];
